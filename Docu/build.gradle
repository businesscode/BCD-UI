/*
  Copyright 2010-2025 BusinessCode GmbH, Germany

  Licensed under the Apache License, Version 2.0 (the "License");
  you may not use this file except in compliance with the License.
  You may obtain a copy of the License at

  http://www.apache.org/licenses/LICENSE-2.0

  Unless required by applicable law or agreed to in writing, software
  distributed under the License is distributed on an "AS IS" BASIS,
  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  See the License for the specific language governing permissions and
  limitations under the License.
*/
//--------------------------------------------------
// Handles generation of HTML documentation from manual adoc and JSDoc comments plus js API stubs for IDEs, based on JSDoc comments
plugins {
  id 'java'
  id "com.github.node-gradle.node"
}

import groovy.json.JsonSlurper


//*********************************************
// Generate classic HTML-JSDoc
// We use Node.js since Rhino is not longer supported since JSDoc 3.4.0.
// (eriwen gradle-js-plugin even supports only a much older JSDoc version, which even frequently with wired errors without source information on simple things like empty comments, we can't use it here)
task bcduiDocuJsHtml ( type: NodeTask, dependsOn: ':bcduiNodeJsTools' ) {

  dependsOn ':Client:bcduiClientCollectAll'

  onlyIf { getGradle().getStartParameter().getTaskNames().contains("bcduiBuildDocu") }

  def inputsDir = "${tasks.findByPath(':Client:bcduiClientCollectAll').destinationDir}/js"
  def targetDir = "$buildDir/htmldocs/jsdoc"
  inputs.dir inputsDir

  // If we are not up-to-date, cleanup before generating
  doFirst {
    delete targetDir
  }

  script = file("$nodeModulesDir/jsdoc/jsdoc.js")
  args   = fileTree( dir: inputsDir, exclude: '**/3rdParty/**' ).collect{it.absolutePath} +
   [ '-c', "$sharedBuildFilesRoot/Docu/gradle/jsdoc-toolkit/htmlConf.json",
     "--recurse",
     "--encoding", "utf8",
     "--template", file("$sharedBuildFilesRoot/.gradle/nodejs/node_modules/jaguarjs-jsdoc").absolutePath,
     "--destination", file(targetDir).absolutePath
   ]
  outputs.dir targetDir

  doLast {

    // Remove generated on timestamp to reduce changes between generations to content
    fileTree( dir: targetDir, includes: ['*.html'] ).each { f ->
      String content = f.getText("UTF-8")
      content = content.replaceAll('Documentation generated by (.*) on.*','Documentation generated by $1')
      content = content.replaceAll('(\r\n|\n|\r)', System.getProperty("line.separator"))
      f.write(content, "UTF-8")
    }

    // We do not like the generated index.html (listing source files with abs path), content of bcdui.html makes much more sense to us as an entry page
    if( file(targetDir).exists() ) {
      def idx = file("$targetDir/index.html")
      def bcd = file("$targetDir/bcdui.html")
      idx.text = bcd.text
    }
    // Overwrite with our custom styles
    copy {
      from "$sharedBuildFilesRoot/Docu/gradle/jsdoc-toolkit/templates/jaguarjs/static"
      into targetDir
    }

    // Dynamically replace version and date when displayed to keep sources as stable as possible
    new File("$targetDir/scripts/jaguar.js").text += """
      \nlet version = "--bcd-dyn-replace-current-version-date--"; document.querySelector('.bcd-dyn-replace-current-version-date').textContent = version;
      """
  }
}



//*********************************************
// For each filegroup, created in Client, we also generate API stubs, suited for Eclipse (version Mars) to support auto-suggest
task bcduiDocuJsApiStubs ( type: NodeTask, dependsOn: ':bcduiNodeJsTools' ) {

  dependsOn ":Client:bcduiClientCollectAll"

  def taskDestDirName = "$buildDir/jsApiStubs"
  def inputsDir       = "${tasks.findByPath(':Client:bcduiClientCollectAll').destinationDir}/js"

  inputs.dir  inputsDir
  outputs.dir taskDestDirName

  onlyIf { getGradle().getStartParameter().getTaskNames().contains("bcduiBuildDocu") }

  // Which files are part of this distribution? We only want this to happen in execution phase
  def jsonSlurper = new JsonSlurper()
  def bcduiFiles = jsonSlurper.parseText(file(jsBcduiLoaderPath).text.split("// JSON-PART-FOR-BUILD")[1])
  def files = bcduiFiles.groups.inject([]){ fullResult, group -> return fullResult + group.files.findAll({ ! it.contains('3rdParty') }) }
  // If a file is included in different fileGroups, just take it once to have all definitions unambiguous
  files = files.unique(false)

  script = file("$nodeModulesDir/jsdoc/jsdoc.js")
  args   = files.collect { tasks.findByPath(':Client:bcduiClientCollectAll').destinationDir.path+it } +
           [ "--recurse",
             "--template", file("$sharedBuildFilesRoot/Docu/gradle/jsdoc-toolkit/templates/bcduiApiStubs").absolutePath,
             "--destination", file(taskDestDirName).absolutePath,
             "--query", "files="+files.join(',') ]
}

//******************************
// Generate Typescript types .d.ts files from api stubs generated above
task bcduiTypescriptDefs ( type: NodeTask, dependsOn: bcduiDocuJsApiStubs ) {

  def outputDir = layout.buildDirectory.dir("tsTypes/node_modules/@types/bcdui/")

  inputs.dir layout.buildDirectory.dir("jsApiStubs")
  outputs.dir outputDir

  workingDir = file("$bcduiDocuJsApiStubs.outputs.files.asPath/bcdui")
  script = file("$sharedBuildFilesRoot/.gradle/nodejs/node_modules/typescript/bin/tsc")
  args   = ["-p", "$sharedBuildFilesRoot/Docu/gradle/jsdoc-toolkit/templates/bcduiApiStubs/tsconfig.json"]

  // tsc generation cleanup
  doLast {
    // Overwrite with a fix index.d.ts as entry for our library
    // Because it references other files, we rename it here at its target place only to prevent IDEs from complaining
    copy {
      from "$sharedBuildFilesRoot/Docu/gradle/jsdoc-toolkit/templates/bcduiApiStubs/index.d.ts_template"
      into outputDir
      rename { "index.d.ts" }
    }
    // Make typedefs available as ambient global types
    def typedefFile = file(outputDir.get().asFile.path+"/typedefs.d.ts")
    def nl = System.lineSeparator()
    typedefFile.text = "// Make globally available${nl}declare global {${nl}${nl}${typedefFile.text}${nl}}${nl}${nl}export{};"
  }
}

//******************************
// Java doc. We skip JAXB generated, as JAXB does produce invalid output in terms of javadoc (xml-parts in its docu)
task bcduiJavadoc(type: Javadoc) {
  destinationDir = file("$buildDir/htmldocs/javadoc")
  source      = findProject(":Server").sourceSets.main.java
  classpath   = findProject(":Server").sourceSets.main.compileClasspath
  verbose     = false
  title       = 'BCD-UI Api'
  options.windowTitle = 'BCD-UI Api'
  options.header      = "<b style=\"font-size: 20pt; color: #fff\">BCD-UI</b>"
  options.addBooleanOption('Xdoclint:none', true)
  options.addStringOption('Xmaxwarns', '1')
  options.addStringOption('stylesheetfile', 'gradle/static/javadoc/stylesheet.css')
  options.noTimestamp = true
}


//******************************
// Creates /build/htmldocs folder with all content meant to be human readable
task bcduiHtmlDocs( type: Copy ) {

  dependsOn bcduiDocuJsHtml, bcduiJavadoc

  from "gradle/static"
  into "$buildDir/htmldocs"
}


//******************************
// Clean artifacts
task bcduiClean( type: Delete ) {
  group "bcd-ui"
  delete buildDir
}

//******************************
// Do all for what gradle 'Docu' project is responsible for

task bcduiBuildDocu {
  group "bcd-ui"
  description "Build docu, see Docu/build, including jsdoc and javadoc."
  dependsOn bcduiHtmlDocs, bcduiTypescriptDefs
}
