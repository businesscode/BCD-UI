/*
  Copyright 2010-2017 BusinessCode GmbH, Germany

  Licensed under the Apache License, Version 2.0 (the "License");
  you may not use this file except in compliance with the License.
  You may obtain a copy of the License at

  http://www.apache.org/licenses/LICENSE-2.0

  Unless required by applicable law or agreed to in writing, software
  distributed under the License is distributed on an "AS IS" BASIS,
  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  See the License for the specific language governing permissions and
  limitations under the License.
*/
//--------------------------------------------------
// Handles compilation of JSP taglib related Java classes
apply plugin: "java" // Because we are also imported as a script, we can't currently (gradle 4.1) use plugins{}
sourceCompatibility = JavaVersion.VERSION_11

dependencies {
  repositories {
    mavenCentral()
    jcenter()
  }
  compileOnly project(':Server')
  compileOnly 'javax.servlet.jsp:jsp-api:2.2'
  compileOnly 'javax.servlet:javax.servlet-api:3.0.1'
}

//*********************************
// Java Plugin default task configuration
jar {

  dependsOn ':ApisGenerator:bcduiBuild'

  baseName = "$artifactsBaseName-jsptaglib"
  version  = bcduiVersion

  // Also add generated tld, tags
  // Tags being part of a jar require a tld file
  into("META-INF") {
    // *.tld files
    from("$jspTaglibRoot/src/WEB-INF") {
      include "webpage.tld";
    }
    from("$jspTaglibRoot/src/META-INF") {
      include "*.tld";
    }
    from "$apisGeneratorRoot/build/gensrc/taglib/META-INF"

    // *.tag files
    into("tags/bcdui") {
      from "$jspTaglibRoot/src/main/taglib"
      from "$apisGeneratorRoot/build/gensrc/taglib/tags"
    }
  }

  manifest {
    attributes( bcduiManifestAttributes() )
  }
  metaInf {
    from bcduiMetaInf
  }
}


//********************************
// Refers to Java Plugin default task
task bcduiClean {
  group = 'bcd-ui'
  dependsOn clean
}

task bcduiBuildJspTaglib {
  dependsOn jar
}